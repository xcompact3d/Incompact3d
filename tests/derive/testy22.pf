module testy22

   use MPI
   use pFunit
   use iso_c_binding
   implicit none

contains

   subroutine log(filename, l1, l2, linf, time)

      use decomp_2d, only : mytype, nrank, nproc, real_type, decomp_2d_abort

      implicit none

      character(len=*), intent(in) :: filename
      real(mytype), intent(in) :: l1, l2, linf, time

      logical :: file_found
      integer :: iounit, code
      real(mytype), dimension(nproc) :: alltime

      alltime(:) = 0.d0
      alltime(nrank+1) = time
      call MPI_ALLREDUCE(MPI_IN_PLACE,alltime,nproc,real_type,MPI_SUM,MPI_COMM_WORLD,code)
      if (code /= 0) call decomp_2d_abort(code, "MPI_ALLREDUCE")

      if (nrank == 0) then
         write(*,*) trim(filename)//" : ", l1, l2, linf, alltime
         inquire(file=filename//".dat", exist=file_found)
         if (file_found) then
            open(newunit=iounit, file=trim(filename)//".dat", form='formatted', status='old', access='append')
         else
            open(newunit=iounit, file=trim(filename)//".dat", form='formatted', status='new')
         endif
         write(iounit, *) l1, l2, linf, alltime
         close(iounit)
      endif

   end subroutine log

   ! Serial test
   @test
   subroutine verif_dery_deryy_22()

      use decomp_2d
      use param
      use var
      use variables
      use tools, only : error_l1_l2_linf
      use ibm_param, only : ubcx,ubcy,ubcz

      implicit none

      real(mytype), dimension(ysize(1),ysize(2),ysize(3)) :: input, output, tmp, sol
      real(mytype) :: y, l1, l2, linf, sl1, sl2, slinf, t1, t2
      integer :: j

      ! Lx = Ly = Lz = 2
      do j = 1, ysize(2)
         y = yp(j)
         input(:,j,:) = sin(5*pi*y)
         sol(:,j,:) = 5*pi*cos(5*pi*y)
      enddo
      call error_l1_l2_linf(sol, sl1, sl2, slinf, ysize(1), ysize(2), ysize(3), xsize(1)*ysize(2)*zsize(3))
      call cpu_time(t1)
      call dery(output,input,di2,sy,ffy,fsy,fwy,ppy,ysize(1),ysize(2),ysize(3),0,ubcx)
      call cpu_time(t2)
      call error_l1_l2_linf(output-sol, l1, l2, linf, ysize(1), ysize(2), ysize(3), xsize(1)*ysize(2)*zsize(3))
      call log("dery22", l1/sl1, l2/sl2, linf/slinf, t2-t1)
      @assertEqual(l1/sl1, 0.d0, tolerance=8.d-10)
      @assertEqual(l2/sl2, 0.d0, tolerance=2.d-8)
      @assertEqual(linf/slinf, 0.d0, tolerance=2.d-7)
      do j = 1, ysize(2)
         y = yp(j)
         input(:,j,:) = cos(5*pi*y)
         sol(:,j,:) = -5*pi*sin(5*pi*y)
      enddo
      call error_l1_l2_linf(sol, sl1, sl2, slinf, ysize(1), ysize(2), ysize(3), xsize(1)*ysize(2)*zsize(3))
      call cpu_time(t1)
      call dery(output,input,di2,sy,ffyp,fsyp,fwyp,ppy,ysize(1),ysize(2),ysize(3),1,ubcy)
      call cpu_time(t2)
      call error_l1_l2_linf(output-sol, l1, l2, linf, ysize(1), ysize(2), ysize(3), xsize(1)*ysize(2)*zsize(3))
      call log("dery22p", l1/sl1, l2/sl2, linf/slinf, t2-t1)
      @assertEqual(l1/sl1, 0.d0, tolerance=3.d-8)
      @assertEqual(l2/sl2, 0.d0, tolerance=4.d-7)
      @assertEqual(linf/slinf, 0.d0, tolerance=6.d-6)

      ! test deryy
      iimplicit = -iimplicit
      do j = 1, ysize(2)
         y = yp(j)
         input(:,j,:) = cos(5*pi*y)
         sol(:,j,:) = -25*(pi**2)*cos(5*pi*y)
      enddo
      call error_l1_l2_linf(sol, sl1, sl2, slinf, ysize(1), ysize(2), ysize(3), xsize(1)*ysize(2)*zsize(3))
      call cpu_time(t1)
      call deryy(output,input,di2,sy,sfyp,ssyp,swyp,ysize(1),ysize(2),ysize(3),1,ubcx)
      if (istret /= 0) then
         call dery(tmp,input,di2,sy,ffyp,fsyp,fwyp,ppy,ysize(1),ysize(2),ysize(3),1,ubcx)
         do j = 1, ysize(2)
            output(:,j,:) = output(:,j,:)*pp2y(j)-pp4y(j)*tmp(:,j,:)
         enddo
      endif
      call cpu_time(t2)
      call error_l1_l2_linf(output-sol, l1, l2, linf, ysize(1), ysize(2), ysize(3), xsize(1)*ysize(2)*zsize(3))
      call log("deryy22p", l1/sl1, l2/sl2, linf/slinf, t2-t1)
      @assertEqual(l1/sl1, 0.d0, tolerance=3.d-8)
      @assertEqual(l2/sl2, 0.d0, tolerance=6.d-8)
      @assertEqual(linf/slinf, 0.d0, tolerance=9.d-7)
      do j = 1, ysize(2)
         y = yp(j)
         input(:,j,:) = sin(5*pi*y)
         sol(:,j,:) = -25*(pi**2)*sin(5*pi*y)
      enddo
      call error_l1_l2_linf(sol, sl1, sl2, slinf, ysize(1), ysize(2), ysize(3), xsize(1)*ysize(2)*zsize(3))
      call cpu_time(t1)
      call deryy(output,input,di2,sy,sfy,ssy,swy,ysize(1),ysize(2),ysize(3),0,ubcy)
      if (istret /= 0) then
         call dery(tmp,input,di2,sy,ffy,fsy,fwy,ppy,ysize(1),ysize(2),ysize(3),0,ubcy)
         do j = 1, ysize(2)
            output(:,j,:) = output(:,j,:)*pp2y(j)-pp4y(j)*tmp(:,j,:)
         enddo
      endif
      call cpu_time(t2)
      call error_l1_l2_linf(output-sol, l1, l2, linf, ysize(1), ysize(2), ysize(3), xsize(1)*ysize(2)*zsize(3))
      call log("deryy22", l1/sl1, l2/sl2, linf/slinf, t2-t1)
      @assertEqual(l1/sl1, 0.d0, tolerance=1.d-7)
      @assertEqual(l2/sl2, 0.d0, tolerance=2.d-6)
      @assertEqual(linf/slinf, 0.d0, tolerance=2.d-5)
      iimplicit = -iimplicit

   end subroutine verif_dery_deryy_22

end module testy22
