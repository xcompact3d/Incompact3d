module testx22

   use MPI
   use pFunit
   use iso_c_binding
   use x3d_unit_testing_tools
   implicit none

contains

   ! Serial test
   @test
   subroutine verif_derx_derxx_22()

      use decomp_2d
      use param
      use var
      use variables
      use tools, only : error_l1_l2_linf
      use ibm_param, only : ubcx,ubcy,ubcz

      implicit none

      real(mytype), dimension(xsize(1),xsize(2),xsize(3)) :: input, output, sol
      real(mytype) :: x, l1, l2, linf, sl1, sl2, slinf, t1, t2
      integer :: i

      ! Lx = Ly = Lz = 2
      do i = 1, xsize(1)
         x = (i-1) * dx
         input(i,:,:) = cos(5*pi*x)
         sol(i,:,:) = - 5*pi*sin(5*pi*x)
      enddo
      call error_l1_l2_linf(sol, sl1, sl2, slinf)
      call cpu_time(t1)
      call derx(output,input,di1,sx,ffxp,fsxp,fwxp,xsize(1),xsize(2),xsize(3),1,ubcx)
      call cpu_time(t2)
      call error_l1_l2_linf(output-sol, l1, l2, linf)
      call log("derx22p", l1/sl1, l2/sl2, linf/slinf, t2-t1)
      if (check_error) then
         @assertEqual(l1/sl1, 0.d0, tolerance=3.d-8)
         @assertEqual(l2/sl2, 0.d0, tolerance=4.d-7)
         @assertEqual(linf/slinf, 0.d0, tolerance=6.d-6)
      endif
      do i = 1, xsize(1)
         x = (i-1) * dx
         input(i,:,:) = sin(5*pi*x)
         sol(i,:,:) = 5*pi*cos(5*pi*x)
      enddo
      call error_l1_l2_linf(sol, sl1, sl2, slinf)
      call cpu_time(t1)
      call derx(output,input,di1,sx,ffx,fsx,fwx,xsize(1),xsize(2),xsize(3),0,ubcy)
      call cpu_time(t2)
      call error_l1_l2_linf(output-sol, l1, l2, linf)
      call log("derx22", l1/sl1, l2/sl2, linf/slinf, t2-t1)
      if (check_error) then
         @assertEqual(l1/sl1, 0.d0, tolerance=8.d-10)
         @assertEqual(l2/sl2, 0.d0, tolerance=2.d-8)
         @assertEqual(linf/slinf, 0.d0, tolerance=2.d-7)
      endif

      ! test derxx
      do i = 1, xsize(1)
         x = (i-1) * dx
         input(i,:,:) = sin(5*pi*x)
         sol(i,:,:) = -25*(pi**2)*sin(5*pi*x)
      enddo
      call error_l1_l2_linf(sol, sl1, sl2, slinf)
      call cpu_time(t1)
      call derxx(output,input,di1,sx,sfx,ssx,swx,xsize(1),xsize(2),xsize(3),0,ubcx)
      call cpu_time(t2)
      call error_l1_l2_linf(output-sol, l1, l2, linf)
      call log("derxx22", l1/sl1, l2/sl2, linf/slinf, t2-t1)
      if (check_error) then
         @assertEqual(l1/sl1, 0.d0, tolerance=1.d-7)
         @assertEqual(l2/sl2, 0.d0, tolerance=2.d-6)
         @assertEqual(linf/slinf, 0.d0, tolerance=2.d-5)
      endif
      do i = 1, xsize(1)
         x = (i-1) * dx
         input(i,:,:) = cos(5*pi*x)
         sol(i,:,:) = -25*(pi**2)*cos(5*pi*x)
      enddo
      call error_l1_l2_linf(sol, sl1, sl2, slinf)
      call cpu_time(t1)
      call derxx(output,input,di1,sx,sfxp,ssxp,swxp,xsize(1),xsize(2),xsize(3),1,ubcy)
      call cpu_time(t2)
      call error_l1_l2_linf(output-sol, l1, l2, linf)
      call log("derxx22p", l1/sl1, l2/sl2, linf/slinf, t2-t1)
      if (check_error) then
         @assertEqual(l1/sl1, 0.d0, tolerance=3.d-8)
         @assertEqual(l2/sl2, 0.d0, tolerance=6.d-8)
         @assertEqual(linf/slinf, 0.d0, tolerance=9.d-7)
      endif

   end subroutine verif_derx_derxx_22

end module testx22
